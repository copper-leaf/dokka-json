apply from: "${rootProject.rootDir}/gradle/groups/formatters.gradle"
apply from: "${rootProject.rootDir}/gradle/actions/shadow.gradle"

dependencies {
    compile project(":common:common-formatter")
    compile project(":swiftdoc:swiftdoc-models")
}

task updateSourceKitten {
    doLast {
        def version = project.property("sourceKittenVersion")
        def tarFile = file("${project.buildDir}/sourcekitten/sourcekitten-${version}.mojave.bottle.tar.gz")
        def tarFileUnpacked = file("${project.buildDir}/sourcekitten/unpacked")
        def unpackedBinary = file("${project.buildDir}/sourcekitten/unpacked/sourcekitten/${version}/bin/sourcekitten")
        def destinationBinaryFolder = file("${project.projectDir}/src/main/resources/bin")

        download {
            src("https://homebrew.bintray.com/bottles/sourcekitten-${version}.mojave.bottle.tar.gz")
            dest(tarFile)
            overwrite(true)
        }

        copy {
            from(tarTree(resources.gzip(tarFile)))
            into(tarFileUnpacked)
        }

        copy {
            from(unpackedBinary)
            into(destinationBinaryFolder)
            fileMode = 0755
        }
    }
}
